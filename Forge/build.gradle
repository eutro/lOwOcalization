buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven {
            url = 'https://repo.spongepowered.org/repository/maven-public/'
            content { includeGroup "org.spongepowered" }
        }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '6.0.6'
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'org.spongepowered.mixin'

archivesBaseName += "-forge"

mixin {
    add sourceSets.main, "lowocalization.refmap.json"
    config "lowocalization.mixins.json"
}

minecraft {
    mappings channel: 'official', version: mcVersion

    runs {
        client {
            workingDirectory file('../run')
            property 'forge.logging.console.level', 'debug'
            property 'fml.earlyprogresswindow', 'false'
            ideaModule "${rootProject.name}.${project.name}.main"
            mods {
                lowocalization {
                    source sourceSets.main
                    source project(":Xplat").sourceSets.main
                }
            }
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${mcVersion}-${forgeVersion}"
    compileOnly project(":Xplat")

    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

compileJava {
    source(project(":Xplat").sourceSets.main.allSource)
    options.compilerArgs << "-Xlint:all,-classfile,-processing,-deprecation,-serial" << "-Werror"
}

processResources {
    from project(":Xplat").sourceSets.main.resources
    inputs.property "version", project.version

    filesMatching("mods.toml") {
        expand "version": project.version
    }
}

jar {
    finalizedBy 'reobfJar'
}
