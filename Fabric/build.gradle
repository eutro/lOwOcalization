plugins {
    id 'fabric-loom' version '1.2.7'
}

archivesBaseName += "-fabric"

loom {
    mixin.defaultRefmapName = "lowocalization.refmap.json"

    runs {
        configureEach {
            runDir "../run"
            ideConfigGenerated(true)
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${mcVersion}"
    mappings loom.layered() {
        officialMojangMappings()
    }

    modImplementation "net.fabricmc:fabric-loader:${fabricLoaderVersion}"

    compileOnly project(":Xplat")
}

compileJava {
    source(project(":Xplat").sourceSets.main.allSource)
    options.compilerArgs << "-Xlint:all,-classfile,-processing,-deprecation,-serial" << "-Werror"
}

processResources {
    from project(":Xplat").sourceSets.main.resources
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        List<Integer> intMcVersion = ((String) mcVersion).split("\\.").collect { Integer.parseInt(it) }
        var mcMinorVer = intMcVersion[1]
        List<Integer> intFLVersion = ((String) fabricLoaderVersion).split("\\.").collect { Integer.parseInt(it) }

        expand(
                "version": project.version,
                "mcVersion": ">=1.${mcMinorVer} <1.${mcMinorVer + 1}",
                "fabricLoaderVersion": ">=${intFLVersion[0]}.${intFLVersion[1]}",
        )
    }
}
